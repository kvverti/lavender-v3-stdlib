##! The prelude contains commonly used and essential functions conveniently
##! imported automatically into every Lavender program compiled with the
##! standard library.

import sys::intrinsic

##! Aliases for intrinsic types.

## The boolean type.
type Bool => intrinsic::Bool

## The float type.
type Flt => intrinsic::Flt

## The integer type.
type Int => intrinsic::Int

## The string type.
type Str => intrinsic::Str

## The function type.
type (->)' => intrinsic::(->)

## The tuple type.
type (,)' => intrinsic::(,)

##! Common functions.

## Identity function.
def id a => a

## Constant function.
def const a => (lam _. a): for b. b -> 'a

## Function application.
def ($)' f a => f a

## Function post-application.
def '(@) => flip ($)

## Function composition.
def '(.)' f g a => f (g a)

## Binary operator transposition.
def flip f a b => f b a

## Curry tuple-accepting function.
def curry f a b => f (a, b)

## Uncurry curried function.
def uncurry f (a, b) => f a b
